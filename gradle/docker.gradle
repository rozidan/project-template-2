buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath('com.bmuschko:gradle-docker-plugin:3.1.0')
    }
}

apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

if (project.hasProperty('dockerUrl')) {
    docker { url = rootProject.dockerUrl }
}

import com.bmuschko.gradle.docker.tasks.image.*

task createDockerfile(type: Dockerfile) {
    group = 'docker'
    description = 'Creates a Dockerfile based on the provided instructions.'
    from 'openjdk:8-jre-alpine'
    maintainer 'maintainer'
    volume("/tmp")
    environmentVariable('PROFILE', '""')
    addFile('"' + jar.archiveName.toString() + '"', '"app.jar"')
    Integer[] ports = [8080]
    if (rootProject.devtools.toBoolean()) {
        ports += 8000
    }
    exposePort(ports)
    String[] entries = [
            'java',
            '-Djava.security.egd=file:/dev/./urandom'
    ]
    if (rootProject.devtools.toBoolean()) {
        entries += [
                '-Xdebug',
                "-Xrunjdwp:server=y,transport=dt_socket,suspend=n,address=8000"
        ]
    }
    entries += ['-jar', '/app.jar']
    entryPoint(entries)
    defaultCommand('--spring.profiles.active=${PROFILE}')
    doFirst {
        copy {
            from jar
            into "build/docker"
        }
    }
}

task buildImage(type: DockerBuildImage) {
    dependsOn build, createDockerfile
    group = 'docker'
    description = 'Builds an image from a Dockerfile.'
    pull true
    inputDir = createDockerfile.destFile.parentFile
    tag = 'template:' + project.version
}