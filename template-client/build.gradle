buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.17"
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.ltgt.apt'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile('com.fasterxml.jackson.core:jackson-annotations')
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.data:spring-data-commons')
    compile('org.springframework:spring-web')
    compile('org.springframework.boot:spring-boot-starter-validation')

    compileOnly('io.springfox:springfox-swagger2:2.9.2')
    compileOnly('org.projectlombok:lombok')
    compileOnly('org.springframework.boot:spring-boot-configuration-processor')

    annotationProcessor('org.projectlombok:lombok')
    annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')

    testCompile('com.fasterxml.jackson.core:jackson-databind')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

compileJava.dependsOn(processResources)

bootJar {
    enabled = false
}

jar {
    enabled = true
    manifest {
        attributes("Implementation-Title": rootProject.name,
                "Implementation-Version": version)
    }
    from("${rootProject.projectDir}") {
        include "LICENSE"
        into "META-INF"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts { archives javadocJar, sourcesJar }

signing {
    required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask(uploadArchives) }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: rootProject.hasProperty('mavenRepo') ? rootProject.mavenRepo : '') {
                authentication(userName: rootProject.hasProperty('mavenUsername') ? rootProject.mavenUsername : '',
                        password: rootProject.hasProperty('mavenPassword') ? rootProject.mavenPassword : '')
            }

            snapshotRepository(url: rootProject.hasProperty('mavenRepoSnapshot') ? rootProject.mavenRepoSnapshot : '') {
                authentication(userName: rootProject.hasProperty('mavenUsername') ? rootProject.mavenUsername : '',
                        password: rootProject.hasProperty('mavenPassword') ? rootProject.mavenPassword : '')
            }

            pom.project {
                name 'Spring Boot Template Starter'
                packaging 'jar'
                description 'Spring boot starter for Template'
                url 'https://server.com/group/spring-boot-starter-template.git'

                scm {
                    connection 'scm:git:https://server.com/group/spring-boot-starter-template.git'
                    developerConnection 'scm:git:https://server.com/group/spring-boot-starter-template.git'
                    url 'https://server.com/group/spring-boot-starter-template.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'userId'
                        name 'user name'
                        email 'user@email.com'
                    }
                }
            }
        }
    }
}
